int "[1-9][0-9]*|0"
float "[0-9]+\\.[0-9]+"
string "\"[^\"]*\""
char "'[^']'"
id "[a-zA-Z_][a-zA-Z0-9_]*"
assign "(<-)|(=)|(:=)|(=>)|(<-)"
lambda_op "(-:)|(:-)"
operator_name "[\\+\\-\\*/%&|^<>!$=\\.]+"
bracket "\\(|\\)|\\[|\\]"
comma ",|\\|"
type_checker_id "[A-Z][a-zA-Z0-9_]*"
.ignore "[ \t\r\n]+"

---

<program> ::= { <expr> | <fun> } ;

<instructions> ::= "(" { <expr> | <fun> } ")"
                 | <fun>
                 ;

<expr> ::= <casted> { <operator> } ( <expr> | <operator> | <fun> )
         | <casted> [ <assign> <expr> ]
         ;

<operator> ::= <operator_name> | "-:" <id> "|" <instructions> "|" <id> ":-" ;

<casted> ::= <int>
           | <float>
           | <string>
           | <char>
           | <id>
           | <instructions>
           | <structure>
           | <list>
           | <comprehensive_list>
           ;

<name> ::= <id> | "operator_name" <operator_name> ;

<fun> ::= <name> [ [ { <expr> "," } ] <expr> ] ;

<comprehensive_list> ::= "[" <expr> "|" <id> "in" <expr> [ "|" <expr> [ "|" <expr> ] ] "]" ;

<structure_if> ::= "if" <expr> <instructions> [ "else" <instructions> ] ;

<structure_while> ::= "while" <expr> <instructions> ;

<structure_loop> ::= "loop" <instructions> ;

<structure_for> ::= "for" <id> "in" <expr> <instructions> ;

<list> ::= "[" [ { <expr> } ] "]" ;

<structure_lambda> ::= [ { [ "&" ] <name> } ] "=>" <instructions> ;

<structure_function> ::= <name> "=" <structure_lambda> ;

<type_checker> ::= <type_checker_id> | <type_checker_id> "[" { <type_checker> | "(" <expr> ")" } "]" ;

<structure_typed_lambda> ::= [ { [ "&" ] <name> ":" <type_checker> } ] "=>" <instructions> ;

<structure_typed_function> ::= <name> "<-" <structure_typed_lambda> ;

<function_call> ::= <id> [ { <expr> } ] ";"
                  | "(" <id> [ { <expr> } ] ")"
                  ;

<structure> ::= <structure_if>
              | <structure_while>
              | <structure_loop>
              | <structure_for>
              | <structure_function>
              | <structure_lambda>
              | <structure_typed_function>
              | <structure_typed_lambda>
              ;
